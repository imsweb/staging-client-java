plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.7.1"
    id 'maven-publish'
    id 'signing'
    id "io.codearte.nexus-staging" version "0.30.0"  // logs into Sonotype OSS and does a "Close" and "Release"
    id 'com.adarshr.test-logger' version '3.0.0'
    id "com.github.ben-manes.versions" version "0.38.0"
    id 'org.sonatype.gradle.plugins.scan' version '2.0.9'
}

group = 'com.imsweb'
version = '6.0.0'
description = 'Java client library for staging calculations'

// fail the build if there are compiler warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()

    registerFeature('updateSupport') {
        usingSourceSet(sourceSets.main)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    spotbugs 'com.github.spotbugs:spotbugs:4.2.3'

    api "com.fasterxml.jackson.core:jackson-core:2.12.3"
    api "com.fasterxml.jackson.core:jackson-annotations:2.12.3"
    api "com.fasterxml.jackson.core:jackson-databind:2.12.3"

    implementation "org.cache2k:cache2k-api:2.0.0.Final"
    runtimeOnly "org.cache2k:cache2k-core:2.0.0.Final"

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.ahocorasick:ahocorasick:0.4.0'

    // split the seerapi-client-java dependency to an optional feature
    updateSupportImplementation 'com.imsweb:seerapi-client-java:3.23'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.algorithms.staging'
        )
    }
}

checkstyle {
    toolVersion = '8.29'
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    // ignore alpha, beta, etc. versions
    rejectVersionIf { selection ->
        isNonStable(selection.candidate.version)
    }
}

// Nexus vulnerability scan (see https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    dependencyGraph = true
    printBanner = false

    // only set proxy if running outside of Github Actions
    if (!System.getenv().GITHUB_ACTIONS) {
        proxyConfiguration {
            protocol = 'http'
            host = 'webproxy-btp.imsweb.com'
            port = 8080
        }
    }
}
check.dependsOn 'ossIndexAudit'

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT')) {
    gradle.startParameter.excludedTaskNames += 'signMavenJavaPublication'
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'staging-client-java'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Staging Java Client'
                description = 'A cancer staging client library'
                url = 'https://github.com/imsweb/staging-client-java'
                inceptionYear = '2015'

                licenses {
                    license {
                        name = 'A modified BSD License (BSD)'
                        url = 'https://github.com/imsweb/staging-client-java/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'ctmay4'
                        name = 'Chuck May'
                        email = 'mayc@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/staging-client-java'
                    connection = 'scm:https://github.com/imsweb/staging-client-java.git'
                    developerConnection = 'scm:git@github.com:imsweb/staging-client-java.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty('nexusUsername') ?: ''
                password = project.findProperty('nexusPassword') ?: ''
            }
        }
    }
}

signing {
    def signingKey = project.findProperty('signingKey') ?: ''
    def signingPassword = project.findProperty('signingPassword') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// configure nexus staging plugin
nexusStaging {
    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}

wrapper {
    gradleVersion = '7.0.1'
    distributionType = Wrapper.DistributionType.ALL
}