plugins {
    id "io.codearte.nexus-staging" version "0.30.0"  // logs into Sonotype OSS and does a "Close" and "Release"
    id "com.github.spotbugs" version "5.0.13"
    id 'com.adarshr.test-logger' version '3.2.0'
    id "com.github.ben-manes.versions" version "0.43.0"
    id 'org.sonatype.gradle.plugins.scan' version '2.5.2'
    id "org.sonarqube" version "3.4.0.2513"
}

sonarqube {
    properties {
        property "sonar.projectKey", "imsweb_staging-client-java"
        property "sonar.organization", "imsweb"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.cpd.exclusions", "**/com/imsweb/staging/entities/impl/**"
        property "sonar.coverage.exclusions", "**/UpdaterUtils.java"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: "com.github.spotbugs"
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: 'org.sonatype.gradle.plugins.scan'
    apply plugin: "org.sonarqube"

    group = 'com.imsweb'
    version = '9.2-SNAPSHOT'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
        testImplementation 'org.assertj:assertj-core:3.23.1'
        testImplementation 'org.zeroturnaround:zt-zip:1.15'
        testImplementation 'com.google.code.bean-matchers:bean-matchers:0.14'
    }

    // fail the build if there are compiler warnings
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion = '8.29'
        configFile = file("../config/checkstyle/checkstyle.xml")
    }

    spotbugs {
        excludeFilter = file("../config/spotbugs/spotbugs-exclude.xml")
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name,
                    'Implementation-Version': archiveVersion,
                    'Implementation-Vendor': group,
                    'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'),
                    'Automatic-Module-Name': 'com.imsweb.algorithms.staging'
            )
        }
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
    test.finalizedBy jacocoTestReport

    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
        def regex = /^[0-9,.v-]+$/
        return !stableKeyword && !(version ==~ regex)
    }

    dependencyUpdates {
        // ignore alpha, beta, etc. versions
        rejectVersionIf { selection ->
            isNonStable(selection.candidate.version)
        }
    }

    // Nexus vulnerability scan (see https://github.com/sonatype-nexus-community/scan-gradle-plugin)
    ossIndexAudit {
        outputFormat = 'DEPENDENCY_GRAPH'
        printBanner = false
    }
    check.dependsOn 'ossIndexAudit'


    // configure nexus staging plugin
    nexusStaging {
        numberOfRetries = 50
        delayBetweenRetriesInMillis = 5000
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    // don't try to release a snapshot to a non-snapshot repository, that won't work anyway
    if (version.endsWith('-SNAPSHOT')) {
        gradle.startParameter.excludedTaskNames += 'signMavenJavaPublication'
        gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'
    }

}

wrapper {
    gradleVersion = '7.5.1'
    distributionType = Wrapper.DistributionType.ALL
}