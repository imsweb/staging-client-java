plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.2.4"
    id "net.ossindex.audit" version "0.4.11"
    id 'com.bmuschko.nexus' version '2.3.1'
    id "com.github.ben-manes.versions" version "0.28.0"
    id "io.codearte.nexus-staging" version "0.21.2"  // logs into Sonotype OSS and does a "Close" and "Release"
    id 'com.adarshr.test-logger' version '2.0.0'
    id "org.owasp.dependencycheck" version "5.3.2.1"
}

group = 'com.imsweb'
version = '4.8-SNAPSHOT'
description = 'Java client library for staging calculations'

// fail the build if there are compiler warnings
tasks.withType(JavaCompile) {
    // UTF-8 for all compilation tasks
    options.encoding = 'UTF-8'

    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-core:2.11.0'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.11.0'
    api 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    // morphia annotations to make it easier to use in SEER*API
    api 'dev.morphia.morphia:core:1.5.8'

    implementation "org.cache2k:cache2k-api:1.2.4.Final"
    implementation "org.cache2k:cache2k-core:1.2.4.Final"
    
    implementation 'org.apache.commons:commons-lang3:3.10'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.16.1'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.algorithms.staging'
        )
    }
}

testlogger {
    theme 'mocha'
}

// auditing plugin
audit {
    failOnError = false
    rateLimitAsError = false

    // exclude test packages
    exclusion {
        packages = [ 'com.puppycrawl.tools:checkstyle', 'org.unitils:unitils-core', 'com.github.spotbugs:spotbugs' ]
    }
}

checkstyle {
    toolVersion = '8.29'
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    toolVersion = '4.0.3'
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

// OWASP check (https://jeremylong.github.io/DependencyCheck/index.html)
dependencyCheck {
    failOnError = true
    format = 'ALL'
}

// when checking for later versions, exclude alphas, betas, etc..
dependencyUpdates {
    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

wrapper {
    gradleVersion = '6.5'
    distributionType = Wrapper.DistributionType.ALL
}

modifyPom {
    project {
        name 'Staging Java Client'
        description 'A cancer staging client library'
        url 'https://github.com/imsweb/staging-client-java'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/staging-client-java'
            connection 'scm:https://github.com/imsweb/staging-client-java.git'
            developerConnection 'scm:git@github.com:imsweb/staging-client-java.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/staging-client-java/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}
